@menu
* Introduction to Plotting::
* Functions and Variables for Plotting::
@end menu

@node Introduction to Plotting, Functions and Variables for Plotting, Plotting, Plotting
@section Introduction to Plotting

Maxima uses an external plotting package to make the plots (see the
section on Plotting Interfaces below). Maxima calculates set of points
to be plotted and pass the to the plotting package together with a set
of commands; the commands and plot data are passed to the external
program either through a pipe or by saving them into a file with the
extension @code{maxout.interface}, where @code{interface} is the name of
the plotting interface being used.

The @code{maxout.interface} file, in the cases when it is used, is
created in the directory specified by the variable @var{temp_dir}. That
location can be changed; by assigning to that variable a string that
represents a valid directory where Maxima can create new files.

After a plot has been obtained, the file @code{maxout.interface} can be
executed again with the appropriate external program. If a Maxima plotting
command fails to show anything, that file can be inspected to look for
possible sources of problems. 

@subsection Plotting Interfaces

Currently, Maxima can one of two external plotting programs: Gnuplot or
Xmaxima. There are various different interfaces for those programs,
which can be selected with the option @code{[plot_format,interface]}, 
where @code{interface} is one of the following:

@itemize @bullet
@item
Value: @code{gnuplot} default on Windows

Gnuplot is the most advanced plotting package among the packages
available in Maxima. It requires an external gnuplot installation. All
plotting commands and data are passed in a file @code{maxout.gnuplot}.

@item
Value: @code{gnuplot_pipes} default on non-Windows platforms

Similar to the @code{gnuplot} format except that the commands are sent
to gnuplot is done through a pipe; the data is passed via a file
@code{maxout.gnuplot_pipes}. It should be used to plot on screen, for
plotting to files it is better to use the @code{gnuplot} format.

@item
Value: @code{mgnuplot}

Mgnuplot is a Tk-based wrapper around gnuplot. It is included in the
Maxima distribution. Mgnuplot offers a rudimentary GUI for gnuplot,
but has fewer overall features than the plain gnuplot
interface. Mgnuplot requires an external gnuplot installation and
Tcl/Tk.

@item
Value: @code{openmath}

Openmath is a Tcl/Tk GUI plotting program. This format is provided by
Xmaxima, which is distributed together with Maxima; in order to use this
format you should install the package Xmaxima, and it will work not only
from Xmaxima itself, but also from the command line and other GUI's for
Maxima. If it is used from within Xmaxima, all commands and data are
passed using the same socket used for the communication between Maxima
and Xmaxima. If it is used from the console or other interface different
from Xmaxima, the commands and data will be save in a file named
@code{maxout.openmath}.

@end itemize

@opencatbox
@category{Plotting}
@closecatbox

@node Functions and Variables for Plotting,  , Introduction to Plotting, Plotting
@section Functions and Variables for Plotting

@deffn {Function} contour_plot (@var{expr}, @var{x_range}, @var{y_range}, @var{options}, ...)

Plots the contours (curves of equal value) of @var{expr}
over the region @var{x_range} by @var{y_range}.
Any additional arguments are treated the same as in @code{plot3d}.

@code{contour_plot} only works when the plot format is @code{gnuplot} or @code{gnuplot_pipes}.

See also @code{implicit_plot}.

Examples:

@c ===beg===
@c contour_plot (x^2 + y^2, [x, -4, 4], [y, -4, 4]);
@c contour_plot (sin(y) * cos(x)^2, [x, -4, 4], [y, -4, 4]);
@c F(x, y) := x^3 + y^2;
@c contour_plot (F, [u, -4, 4], [v, -4, 4]);
@c contour_plot (F, [u, -4, 4], [v, -4, 4], [gnuplot_preamble, 
@c       "set size ratio -1"]);
@c set_plot_option ([gnuplot_preamble, 
@c       "set cntrparam levels 12"])$
@c contour_plot (F, [u, -4, 4], [v, -4, 4]);
@c ===end===
@example
(%i1) contour_plot (x^2 + y^2, [x, -4, 4], [y, -4, 4]);
(%o1) 
(%i2) contour_plot (sin(y) * cos(x)^2, [x, -4, 4], [y, -4, 4]);
(%o2) 
(%i3) F(x, y) := x^3 + y^2;
                                   3    2
(%o3)                  F(x, y) := x  + y
(%i4) contour_plot (F, [u, -4, 4], [v, -4, 4]);
(%o4) 
(%i5) contour_plot (F, [u, -4, 4], [v, -4, 4], [gnuplot_preamble,
      "set size ratio -1"]);
(%o5) 
(%i6) set_plot_option ([gnuplot_preamble,
                        "set cntrparam levels 12"])$

(%i7) contour_plot (F, [u, -4, 4], [v, -4, 4]);
@end example

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

@c INSPECTING src/plot.lisp AND TRYING SOME EXAMPLES,
@c IT APPEARS THAT in_netmath HAS NO EFFECT ON plot2d
@c AND ONLY EFFECT ON plot3d IS TO CAUSE OPENMATH OUTPUT TO BE WRITTEN TO CONSOLE
@c WHEN [plot_format, openmath] IS SET.
@c NOT CONVINCED WE REALLY WANT TO DOCUMENT THIS VARIABLE
@defvr {Option variable} in_netmath
Default value: @code{false}

When @code{in_netmath} is @code{true},
@code{plot3d} prints OpenMath output to the console if @code{plot_format} is @code{openmath};
otherwise @code{in_netmath} (even if @code{true}) has no effect.
@code{in_netmath} has no effect on @code{plot2d}.

@end defvr

@deffn {Function} plot2d (@var{expr}, @var{x_range}, ..., @var{options}, ...)
@deffnx {Function} plot2d ([@var{expr_1}, ..., @var{expr_n}], ..., @var{options}, ...)
@deffnx {Function} plot2d ([@var{expr_1}, ..., @var{expr_n}], @var{x_range},..., @var{options}, ...)

Where @var{expr}, @var{expr_1}, ..., @var{expr_n} can be either
expressions, or Maxima or Lisp functions or operators, or a list with
the any of the forms: @code{[discrete, [@var{x1}, ..., @var{xn}],
[@var{y1}, ..., @var{yn}]]}, @code{[discrete, [[@var{x1}, @var{y1}],
..., [@var{xn}, ..., @var{yn}]]} or @code{[parametric, @var{x_expr},
@var{y_expr}, @var{t_range}]}.

Displays a plot of one or more expressions as a function of one
variable.

@code{plot2d} plots one expression @var{expr} or several expressions
@code{[@var{name_1}, ..., @var{name_n}]}. The expressions that are not
of the parametic or discrete types should all depend only on one
variable @var{var} and it will be mandatory the use of @var{x_range} to
name that variable and gives its minimum and maximum values, using the
syntax: @code{[@var{variable}, @var{min}, @var{max}]}. The plot will
show the horizontal axis bound by the values of @var{min} and @var{max}.

A expression to be plotted can also be given in the discrete or
parametric forms. Namely, as a list starting with the word ``discrete''
or ``parametric''. The keyword @var{discrete} must be followed by two
lists of values, both with the same length, which are the horizontal and
vertical coordinates of a set of points; alternatively, the coordinates
of each point can be put into a list with two values, and all the
coordinates of the points should be inside another list. The keyword
@var{parametric} must be followed by two expressions @var{x_expr} and
@var{y_expr}, and a range of the form @code{[@var{param}, @var{min},
@var{max}]}. The two expressions must depend only on the parameter
@var{param}, and the plot will show the path traced out by the point
with coordinates (@var{x_expr}, @var{y_expr}) as @var{param} increases
from @var{min} to @var{max}.

The range of the vertical axis is not mandatory. It is one more of the
options for the command, with the syntax: @code{[y, @var{min},
@var{max}]}. If that option is used, the plot will show that entire
range, even if the expressions do not reach all that range. Otherwise,
if a vertical range is not specified by @code{set_plot_option}, the
boundaries of the vertical axis will be set up automatically.

All other options should also be lists, starting with the name of the
option. The option @var{xlabel} can be used to give a label for the
horizontal axis; if that option is not used, the horizontal axis will
be labeled with the name of the variable specified in @var{x_range}, or
with the expression @var{x_expr} in the case of just one parametric
expression, or it will be left blank otherwise.

A label for the vertical axis can be given with the @var{ylabel}
option. If there is only one expression to be plotted and the
@var{ylabel} option was not used, the vertical axis will be labeled with
that expression, unless it is too large, or with the expression
@var{y_expr} if the expression is parametric, or with the text
``discrete data'' if the expression is discrete.

The options @code{[logx]} and @code{[logy]} do not need any
parameters. They will make the horizontal and vertical axes be
scaled logarithmically.

If there are several expressions to be plotted, a legend will be
written to identiy each of the expressions. The labels that should be
used in that legend can be given with the option @var{legend}. If that
option is not used, Maxima will create labels from the expressions.

By default, the expressions are plotted as a set of line segments
joining adjacent points within a set of points which is either given in
the @var{discrete} form, or calculated automatically from the expression
given, using an algorithm that automatically adapts the steps among
points using as an initial estimate of the total number of points the
value set with the @var{nticks} option. The option @var{style} can be
used to make one of the expressions to be represented as a set of
isolated points, or as points and line segments.

There are several global options stored in the list @var{plot_options}
which can be modified with the function @code{set_plot_option}; any of
those global options can be overriden with options given in the
@var{plot2d} command. There are also other options which are not part of
the global options array @var{plot_options} and which can be given only
as part of the @var{plot2d} command; those options are: @var{logx},
@var{logy}, @var{box}, @var{legend}, @var{xlabel}, @var{ylabel},
@var{psfile} and @var{style}.

A function to be plotted may be specified as the name of a Maxima or
Lisp function or operator, a Maxima lambda expression, or a general
Maxima expression.  If specified as a name or a lambda expression, the
function must be a function of one argument.

@c PUT EXAMPLES FOR PRECEDING SIMPLE FORMS OF plot2d HERE
@b{Examples:}

Plots of common functions.

@c ===beg===
@c plot2d (sin(x), [x, -5, 5])$
@c plot2d (sec(x), [x, -2, 2], [y, -20, 20], [nticks, 200])$
@c ===end===
@example
(%i1) plot2d (sin(x), [x, -5, 5])$

(%i2) plot2d (sec(x), [x, -2, 2], [y, -20, 20], [nticks, 200])$
@end example

@ifnotinfo
@image{figures/plotting2,8cm}@image{figures/plotting3,8cm}
@end ifnotinfo

Plotting functions by name.

@c ===beg===
@c F(x) := x^2 $
@c :lisp (defun |$g| (x) (m* x x x))
@c H(x) := if x < 0 then x^4 - 1 else 1 - x^5 $
@c plot2d (F, [u, -1, 1])$
@c plot2d ([F, G, H], [u, -1, 1], [y, -1.5, 1.5])$
@c ===end===
@example
(%i3) F(x) := x^2 $

(%i4) :lisp (defun |$g| (x) (m* x x x))

$g
(%i5) H(x) := if x < 0 then x^4 - 1 else 1 - x^5 $

(%i6) plot2d (F, [u, -1, 1])$

(%i7) plot2d ([F, G, H], [u, -1, 1], [y, -1.5, 1.5])$
@end example

@ifnotinfo
@image{figures/plotting4,8cm}@image{figures/plotting5,8cm}
@end ifnotinfo

We can plot a circle using a parametric plot with a parameter
@var{t}. It is not necessary to give a range for the horizontal range,
since the range of the parameter @var{t} determines the
domain. However, since the graph's horizontal and vertical axes
lengths are in the 4 to 3 proportion, we will use the @var{xrange}
option to obtain the same scaling in both axes:

@c ===beg===
@c plot2d ([parametric, cos(t), sin(t), [t, -%pi, %pi], 
@c       [nticks, 80]], [x, -4/3, 4/3])$
@c ===end===
@example
(%i8) plot2d ([parametric, cos(t), sin(t), [t,-%pi,%pi],
      [nticks,80]], [x, -4/3, 4/3])$
@end example

@ifnotinfo
@image{figures/plotting6,8cm}
@end ifnotinfo

If we repeat that plot with only 8 points and extending the range
of the parameter to give two turns, we will obtain the plot of a star:

@c ===beg===
@c plot2d ([parametric, cos(t), sin(t), [t, -%pi*2, %pi*2], 
@c         [nticks, 8]], [x, -2, 2], [y, -1.5, 1.5])$
@c ===end===
@example
(%i9) plot2d ([parametric, cos(t), sin(t), [t, -%pi*2, %pi*2],
        [nticks, 8]], [x, -2, 2], [y, -1.5, 1.5])$
@end example

@ifnotinfo
@image{figures/plotting7,8cm}
@end ifnotinfo

Combination of an ordinary plot of a cubic polynomial with a
parametric plot of a circle:

@c ===beg===
@c plot2d ([x^3+2, [parametric, cos(t), sin(t), [t, -5, 5], 
@c         [nticks, 80]]], [x, -3, 3])$
@c ===end===
@example
(%i10) plot2d ([x^3+2, [parametric, cos(t), sin(t), [t, -5, 5],
        [nticks, 80]]], [x, -3, 3])$
@end example

@ifnotinfo
@image{figures/plotting8,8cm}
@end ifnotinfo

Example of a logarithmic plot:
@c ===beg===
@c plot2d (exp(3*s), [s, -2, 2], [logy])$
@c ===end===
@example
(%i11) plot2d (exp(3*s), [s, -2, 2], [logy])$
@end example

@ifnotinfo
@image{figures/plotting9,8cm}
@end ifnotinfo

To show some examples of discrete plots, we will start by entering the
coordinates of 5 points, in the two different ways that can be used:

@c ===beg===
@c xx:[10, 20, 30, 40, 50]$
@c yy:[.6, .9, 1.1, 1.3, 1.4]$
@c xy:[[10,.6], [20,.9], [30,1.1], [40,1.3], [50,1.4]]$
@c ===end===
@example
(%i12) xx:[10, 20, 30, 40, 50]$
(%i13) yy:[.6, .9, 1.1, 1.3, 1.4]$
(%i14) xy:[[10,.6], [20,.9], [30,1.1], [40,1.3], [50,1.4]]$
@end example

To plot those data points, joined with line segments, we use:
@c ===beg===
@c plot2d([discrete,xx,yy])$
@c ===end===
@example
(%i15) plot2d([discrete,xx,yy])$
@end example

@ifnotinfo
@image{figures/plotting10,8cm}
@end ifnotinfo

We will now show the plot with only points, and illustrating the use of
the second way of giving the points coordinates:

@c ===beg===
@c plot2d([discrete, xy], [style, points])$
@c ===end===
@example
(%i16) plot2d([discrete, xy], [style, points])$
@end example

@ifnotinfo
@image{figures/plotting11,8cm}
@end ifnotinfo

The plot of the data points can be shown together with a plot of the
theoretical function that predicts the data:

@c ===beg===
@c plot2d([[discrete,xy], 2*%pi*sqrt(l/980)], [l,0,50], 
@c         [style, [points,5,2,6], [lines,1,1]],
@c         [legend,"experiment","theory"],
@c         [xlabel,"pendulum's length (cm)"], [ylabel,"period (s)"])$
@c ===end===
@example
(%i17) plot2d([[discrete,xy], 2*%pi*sqrt(l/980)], [l,0,50],
        [style, [points,5,2,6], [lines,1,1]],
        [legend,"experiment","theory"],
        [xlabel,"pendulum's length (cm)"], [ylabel,"period (s)"])$
@end example

@ifnotinfo
@image{figures/plotting12,8cm}
@end ifnotinfo

The meaning of the three numbers after the ``points'' style option
are as follows; 5: radius of the points, 2: index of color used (red),
6: type of objects used (solid squares). The two numbers after the
``lines'' style option give the thickness of the line (1 point) and
the color (1 corresponds to blue).

To save a plot of @code{sin(x)} to the file @code{sin.ps}, one can use:
@example
(%i18) plot2d (sin(x), [x, 0, 2*%pi], [psfile, "sin.eps"])$
@end example

The next example uses the y option to chop off singularities and the
gnuplot_preamble option to put the key at the bottom of the plot instead
of the top.
@example
(%i19) plot2d ([gamma(x), 1/gamma(x)], [x, -4.5, 5], [y, -10, 10],
                     [gnuplot_preamble, "set key bottom"])$
@end example

@ifnotinfo
@image{figures/plotting14,8cm}
@end ifnotinfo

We can also use a @code{gnuplot_preamble} to produce fancy x-axis
labels.  (Note that the @code{gnuplot_preamble} string must be entered
without any line breaks.)
@example
(%i20) my_preamble: "set xtics ('-2pi' -6.283, \
'-3pi/2' -4.712, '-pi' -3.1415, '-pi/2' -1.5708, '0' 0, \
'pi/2' 1.5708, 'pi' 3.1415,'3pi/2' 4.712, '2pi' 6.283)"$

(%i21) plot2d([cos(x), sin(x), tan(x), cot(x)],
       [x, -2*%pi, 2.1*%pi], [y, -2, 2], [axes, x],
       [gnuplot_preamble, my_preamble]);
@end example

@ifnotinfo
@image{figures/plotting15,8cm}
@end ifnotinfo

This example uses a @code{gnuplot_preamble} to produce fancy x-axis
labels, and produces PostScript output that takes advantage of the
advanced text formatting available in gnuplot.  (Note that the
@code{gnuplot_preamble} string must be entered without any line breaks.)
@example
(%i22) my_preamble: "set xtics ('-2@{/Symbol p@}' \
-6.283, '-3@{/Symbol p@}/2' -4.712, '-@{/Symbol p@}' -3.1415, \
'-@{/Symbol p@}/2' -1.5708, '0' 0,'@{/Symbol p@}/2' 1.5708, \
'@{/Symbol p@}' 3.1415,'3@{/Symbol p@}/2' 4.712, '2@{/Symbol p@}' \
6.283)"$

(%i23) plot2d ([cos(x), sin(x), tan(x)], [x, -2*%pi, 2*%pi],
    [y, -2, 2], [gnuplot_preamble, my_preamble],
    [psfile, "trig.eps"]);
@end example

See also @code{plot_options}, which describes plotting options.

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

@defvr {System variable} plot_options
Elements of this list state the default options for plotting.
If an option is present in a @code{plot2d} or @code{plot3d} call,
that value takes precedence over the default option.
Otherwise, the value in @code{plot_options} is used.
Default options are assigned by @code{set_plot_option}. There are other
local options specific to each plotting command, and not included in
this list of global options.

Each element of @code{plot_options} is a list of two or more items.
The first item is the name of an option, and the remainder comprises the value or values
assigned to the option.
In some cases the, the assigned value is a list, which may comprise several items.

The plot options which are recognized by @code{plot2d} and @code{plot3d} are the following:

@itemize @bullet
@item
Option: @code{plot_format}

Determines which graphic interface is used by @code{plot2d} and
@code{plot3d}. It can be either @code{gnuplot}, @code{gnuplot_pipes},
@code{mgnuplot} or @code{openmath}

@item
Option: @code{run_viewer}

Controls whether or not the appropriate viewer for the plot
format should be run.

@itemize @bullet
@item
@c DOES FALSE IMPLY THE OUTPUT FILE IS GENERATED AND NOT SHOWN ?? OR IS NOTHING GENERATED ??
Default value: @code{true}

Execute the viewer program.
@item
Value: @code{false}

Do not execute the viewer program.
@end itemize

@item
Option: @code{y}

The vertical range of the plot.

Example:

@example
[y, - 3, 3]
@end example
Sets the vertical range to [-3, 3].

@item
Option: @code{plot_realpart}

When @code{plot_realpart} is @code{true},
the real part of a complex value @var{x} is plotted;
this is equivalent to plotting @code{realpart(@var{x})} instead of @var{x}.
Otherwise,
only values with imaginary part equal to 0 are plotted,
and complex values are ignored.

Example:

@example
plot2d (log(x), [x, -5, 5], [plot_realpart, false]);
plot2d (log(x), [x, -5, 5], [plot_realpart, true]);
@end example
The default value is @code{false}.

@item
Option: @code{nticks}

In plot2d, it is gives the initial number of points used by the
adaptive plotting routine for plotting functions. It is also the
number of points that will be shown in a parametric plot.

Example:

@example
[nticks, 20]
@end example
The default for @code{nticks} is 29.

@item
Option: @code{adapt_depth}

The maximum number of splittings used by the adaptive plotting routine.

Example:

@example
[adapt_depth, 5]
@end example
The default for @code{adapt_depth} is 10.

@item
Option: @code{xlabel}

The label for the horizontal axis in a 2d plot.

Example:
@example
[xlabel, "Time in seconds"]
@end example

@item
Option: @code{ylabel}

The label of the vertical axis in a 2d plot.

Example:
@example
[ylabel, "Temperature"]
@end example

@item
Option: @code{logx}

It makes the horizontal axis of a 2d plot to be rendered in a
logarithmic scale. It does not need any aditional parameters.

@item
Option: @code{logy}

It makes the vertical axis of a 2d plot to be rendered in a
logarithmic scale. It does not need any aditional parameters.

@item
Option: @code{legend}

The labels for the various expressions in a 2d plot with several
expressions. If there are more expressions than the number of labels
given, they will be repeated. If legend is followed by the word
@emph{false}, no legend will be shown. By default, the names of the
expressions or functions will be used, or the words discrete1,
discrete2, ..., for discrete sets of points.

Example:
@example
[legend, "Set 1", "Set 2", "Set 3"]
@end example

@item
Option: @code{box}       
                 
Currently, this option can only be followed by the word @emph{false},    
and it will be used to supress the box around the plot.

Example:
@example
[box, false]
@end example

@item
Option: @code{style}

The styles that will be used for the various functions or sets of data
in a 2d plot. The word @var{style} must be followed by one or more
styles. If there are more functions and data sets than the styles
given, the styles will be repeated. Each style can be either
@var{lines} for line segments, @var{points} for isolated points,
@var{linespoints} for segments and points, or @var{dots} for small
isolated dots. Gnuplot accepts also an @var{impulses} style.

Each of the styles can be enclosed inside a list with some aditional
parameters. @var{lines} accepts one or two numbers: the width of the
line and an integer that identifies a color. The default color codes
are: 1: blue, 2: red, 3: magenta, 4: orange, 5: brown, 6: lime and 7:
aqua. If you use Gnuplot with a terminal different than X11,
those colors might be different; for example, if you use the option
[@var{gnuplot_term},@var{ps}], color index 4 will correspond to black,
instead of orange.

@var{points} accepts one two or three parameters; the first parameter
is the radius of the points, the second parameter is an integer that
selects the color, using the same code used for @var{lines} and the
third parameter is currently used only by Gnuplot and it corresponds
to several objects instead of points. The default types of
objects are: 1: filled circles, 2: open circles, 3: plus signs, 4: x,
5: *, 6: filled squares, 7: open squares, 8: filled triangles, 9: open
triangles, 10: filled inverted triangles, 11: open inverted triangles,
12: filled lozenges and 13: open lozenges.

@var{linesdots} accepts up to four parameters: line width, points
radius, color and type of object to replace the points.

Example:
@example
[style,[lines,2,3],[points,1,4,3]]
@end example

This will plot the first (and third, fifth, etc) expression with
magenta line segments of width 2, and the second (and fourth, sixth,
etc) expression with orange plus signs of size 1 (orange circles in
the case of Openmath).

The default for the style option is @var{lines} with a width of 1, and
different colors.

@item
Option: @code{psfile}       
                 
This option is used to save the plots created by plot2d or plot3d into a
Postscript file. The option should be given one parameter, which must be
a valid name for a Postscript file. By default, that file will be
created in the directory defined by the variable @var{maxima_tempdir};
the value of that variable can be changed to save the file in a
different directory.

Example:
@example
[psfile, "plot1.ps"]
@end example

@item
Option: @code{axes}

This is a global option used only by plot2d. By default it has the value
@emph{true}, which means that the two axis, x and y, will be
displayed. It can be changed to @emph{x}, to show only the x axis, or to
@emph{y}, which will show only the y axis, or to @emph{false} to skip
the display of the axes.

Example:
@example
[axes, 'x]
@end example

@item
Option: @code{grid}

Sets the number of grid points to use in the x- and y-directions
for three-dimensional plotting.

Example:
@example
[grid, 50, 50]
@end example
sets the grid to 50 by 50 points. The default grid is 30 by 30.

@item
Option: @code{transform_xy}

Allows transformations to be applied to three-dimensional plots.

Example:
@example
[transform_xy, false]
@end example
The default @code{transform_xy} is @code{false}. If it is not @code{false}, it should be
the output of

@example
make_transform([x,y,z], f1(x,y,z), f2(x,y,z), f3(x,y,z))$
@end example
The @code{polar_xy} transformation is built in. It gives the same
transformation as
@example
make_transform ([r, th, z], r*cos(th), r*sin(th), z)$
@end example
@end itemize

@b{Gnuplot options:}

There are several plot options specific to gnuplot.  Some of these
options are raw gnuplot commands, specified as strings. Refer to the
gnuplot documentation for more details. In most cases, these options can
be replaced by one of the more general options above; in those cases we
recommend that you use the most general form.

@itemize @bullet
@item
Option: @code{gnuplot_term}

Sets the output terminal type for gnuplot.
@itemize @bullet
@item
Default value: @code{default}

Gnuplot output is displayed in a separate graphical window.

@item
Value: @code{dumb}

Gnuplot output is displayed in the Maxima console by an "ASCII art" approximation to graphics.

@item
Value: @code{ps}

Gnuplot generates commands in the PostScript page description language.
If the option
@code{gnuplot_out_file} is set to @var{filename}, gnuplot writes the PostScript commands to @var{filename}.
Otherwise, it is saved as @code{maxplot.ps} file.

@item
Value: any other valid gnuplot term specification

Gnuplot can generate output in many other graphical formats such
as png, jpeg, svg etc.  To create plot in all these formats the
@code{gnuplot_term} can be set to any supported gnuplot term name (symbol) 
or even full gnuplot term specification with any valid options (string).
For example @code{[gnuplot_term,png]} creates output in PNG (Portable
Network Graphics) format while @code{[gnuplot_term,"png size 1000,1000"]}
creates PNG of 1000x1000 pixels size. 
If the option @code{gnuplot_out_file} is set to @var{filename}, gnuplot 
writes the output to @var{filename}. Otherwise, it is saved as 
@code{maxplot.@var{term}} file, where @var{term} is gnuplot 
terminal name.
@end itemize

@item
Option: @code{gnuplot_out_file}

When used in conjunction with the @code{gnuplot_term} option, it can be
used to save the plot in a file, in one of the graphic formats supported
by Gnuplot. If you want to create a Postscript file, you can use the
option @code{psfile} instead, which will also work in Openmath, and does
the same thing with just one option.

@example
[gnuplot_term, png], [gnuplot_out_file, "graph3.png"]
@end example

@item
Option: @code{gnuplot_pm3d}

Controls the usage PM3D mode, which has advanced 3D
features. PM3D is only available in gnuplot versions after 3.7. The
default value for @code{gnuplot_pm3d} is @code{false}.

Example:

@example
[gnuplot_pm3d, true]
@end example

@item
Option: @code{gnuplot_preamble}

Inserts gnuplot commands before the plot is
drawn. Any valid gnuplot commands may be used. Multiple commands
should be separated with a semi-colon. The example shown produces a
log scale plot. The default value for @code{gnuplot_preamble} is the empty string @code{""}.

Example:

@example
[gnuplot_preamble, "set size ratio -1"]
@end example

@item
Option: @code{gnuplot_curve_titles}

This is an old option that has been replaced by @emph{legend} described
above.

@item
Option: @code{gnuplot_curve_styles}

This is an old option that has been replaced by @emph{style} described
above.

@item
Option: @code{gnuplot_default_term_command}

The gnuplot command to set the
terminal type for the default terminal. The default value is @code{set
term windows "Verdana" 15} in Windows systems, and @code{set term x11
font "Helvetica,16"} in X11 windows systems.

Example:

@example
[gnuplot_default_term_command, "set term x11"]
@end example

@item
Option: @code{gnuplot_dumb_term_command}

The gnuplot command to set the
terminal type for the dumb terminal. The default value is @code{"set term dumb 79 22"},
which makes the text output 79 characters by 22
characters.

Example:

@example
[gnuplot_dumb_term_command, "set term dumb 132 50"]
@end example

@item
Option: @code{gnuplot_ps_term_command}

The gnuplot command to set the terminal
type for the PostScript terminal. The default value is
@code{"set size 1.5, 1.5;set term postscript eps enhanced color solid 24"},
which sets the
size to 1.5 times gnuplot's default, and the font size to 24, among
other things. See the gnuplot documentation for @code{set term postscript} for more information.

Example:

All the figures in the examples for the @var{plot2d} function in this
manual were obtained from Postscript files that were generated after
setting @code{gnuplot_ps_term_command} as:

@example
[gnuplot_ps_term_command, "set size 1.3, 1.3; \
        set term postscript eps color solid lw 2.5 30"]
@end example

@end itemize

@opencatbox
@category{Plotting}
@closecatbox

@end defvr

@deffn {Function} plot3d ([@var{expr_1}, @var{expr_2}, @var{expr_3}], @var{x_range}, @var{y_range}, ..., @var{options}, ...)
@deffnx {Function} plot3d (@var{expr}, @var{x_range}, @var{y_range}, ..., @var{options}, ...)
@deffnx {Function} plot3d (@var{name}, @var{x_range}, @var{y_range}, ..., @var{options}, ...)
@deffnx {Function} plot3d ([@var{expr_1}, @var{expr_2}, @var{expr_3}], @var{x_rge}, @var{y_rge})
@deffnx {Function} plot3d ([@var{name_1}, @var{name_2}, @var{name_3}], @var{x_range}, @var{y_range}, ..., @var{options}, ...)

Displays a plot of one or three expressions as functions of two variables.

@example
(%i1) plot3d (2^(-u^2 + v^2), [u, -3, 3], [v, -2, 2]);
@end example

@ifnotinfo
@image{figures/plotting19,8cm}
@end ifnotinfo

plots @code{z = 2^(-u^2+v^2)} with @code{u} and @code{v} varying in [-3,3] and
[-2,2] respectively, and with @var{u} on the x axis, and @code{v} on the y
axis.

The same graph can be plotted using openmath (if Xmaxima is installed):

@example
(%i2)  plot3d (2^(-u^2 + v^2), [u, -3, 3], [v, -2, 2],
               [plot_format, openmath]);
@end example

@ifnotinfo
@image{figures/plotting25,8cm}
@end ifnotinfo

@noindent
in this case the mouse can be used to rotate the plot to look at the surface
from different sides.

An example of the third pattern of arguments is

@example
(%i3) plot3d ([cos(x)*(3 + y*cos(x/2)), sin(x)*(3 + y*cos(x/2)),
   y*sin(x/2)], [x, -%pi, %pi], [y, -1, 1], ['grid, 50, 15]);
@end example

@ifnotinfo
@image{figures/plotting20,8cm}
@end ifnotinfo

which plots a Moebius band, parametrized by the three expressions given
as the first argument to @code{plot3d}.  An additional optional argument
@code{['grid, 50, 15]} gives the grid number of rectangles in the x direction and
y direction.

The function to be plotted
may be specified as the name of a Maxima or Lisp function or operator,
a Maxima lambda expression, or a general Maxima expression.
In the form @code{plot3d (@var{f}, ...)} where @var{f} is the
name of a function or a lambda expression,
the function must be a function of two arguments.
In the form @code{plot3d ([@var{f_1}, @var{f_2}, @var{f_3}], ...)}
where @var{f_1}, @var{f_2}, and @var{f_3} are names of functions or lambda expressions,
each function must be a function of three arguments.

This example shows a plot of the real part of @code{z^1/3}, using polar
coordinates. The options @code{box} and @code{legend} were also used to
suppress the title and the axes box. 

@example
(%i4) plot3d (r^.33*cos(th/3), [r, 0, 1], [th, 0, 6*%pi],
      ['grid, 12, 80], ['transform_xy, polar_to_xy],
      [box, false],[legend,false]);
@end example

@ifnotinfo
@image{figures/plotting21,8cm}
@end ifnotinfo

Other examples are the Klein bottle:

@example
(%i5) expr_1: 5*cos(x)*(cos(x/2)*cos(y) + sin(x/2)*sin(2*y)
      + 3.0) - 10.0$
(%i6) expr_2: -5*sin(x)*(cos(x/2)*cos(y) + sin(x/2)*sin(2*y)
      + 3.0)$
(%i7) expr_3: 5*(-sin(x/2)*cos(y) + cos(x/2)*sin(2*y))$

(%i8) plot3d ([expr_1, expr_2, expr_3], [x, -%pi, %pi],
      [y, -%pi, %pi], ['grid, 40, 40]);
@end example

@ifnotinfo
@image{figures/plotting22,8cm}
@end ifnotinfo

and a torus:

@example
(%i9) expr_1: cos(y)*(10.0+6*cos(x))$
(%i10) expr_2: sin(y)*(10.0+6*cos(x))$
(%i11) expr_3: -6*sin(x)$
(%i12) plot3d ([expr_1, expr_2, expr_3], [x, 0, 2*%pi],
       [y, 0, 2*%pi], ['grid, 40, 40]);
@end example

@ifnotinfo
@image{figures/plotting23,8cm}
@end ifnotinfo

Sometimes it is necessary to define a function to plot the expression. All
the arguments to plot3d are evaluated before being passed to plot3d, and
so trying to make an expression which does just what is needed may be
difficult, and it is just easier to make a function.   

@example
(%i13) M: matrix([1, 2, 3, 4], [1, 2, 3, 2], [1, 2, 3, 4],
       [1, 2, 3, 3])$
(%i14) f(x, y) := float (M [?round(x), ?round(y)])$
(%i15) plot3d (f, [x, 1, 4], [y, 1, 4], ['grid, 4, 4])$
@end example

@ifnotinfo
@image{figures/plotting24,8cm}
@end ifnotinfo

Here is a three-dimensional plot using the gnuplot pm3d terminal.
@example
(%i7) plot3d (atan (-x^2 + y^3/4), [x, -4, 4], [y, -4, 4],
        [grid, 50, 50], [gnuplot_pm3d, true])$
@end example

@ifnotinfo
@image{figures/plotting16,8cm}
@end ifnotinfo

And a three-dimensional plot without a mesh and with contours projected
on the bottom plane.  (Note that the @code{gnuplot_preamble} string must
be entered without any line breaks.)

@example
(%i8) my_preamble: "set pm3d at s;unset surface;set contour;\
set cntrparam levels 20;unset key"$
(%i9) plot3d(atan(-x^2 + y^3/4), [x, -4, 4], [y, -4, 4],
    [grid, 50, 50], [gnuplot_pm3d, true],
    [gnuplot_preamble, my_preamble])$
@end example

@ifnotinfo
@image{figures/plotting17,8cm}
@end ifnotinfo

Finally, a plot where the z-axis is represented by color only.
@example
(%i10) plot3d (cos (-x^2 + y^3/4), [x, -4, 4], [y, -4, 4],
    [gnuplot_preamble, "set view map; unset surface"],
    [gnuplot_pm3d, true], [grid, 150, 150])$
@end example

@ifnotinfo
@image{figures/plotting18,8cm}
@end ifnotinfo

See the available options in @code{plot_options}.

@opencatbox
@category{Plotting}
@closecatbox

@end deffn


@deffn {Function} make_transform (@var{vars}, @var{fx}, @var{fy}, @var{fz})
Returns a function suitable for the transform function in plot3d. Use
with the plot option @code{transform_xy}.
@example
make_transform ([r, th, z], r*cos(th), r*sin(th), z)$
@end example
is a transformation to polar coordinates.

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

@deffn {Function} set_plot_option (@var{option})
Assigns one of the global variables for plotting.
@var{option} is specified as a list of two or more elements,
in which the first element is one of the keywords
on the @code{plot_options} list.

@code{set_plot_option} evaluates its argument and
returns the complete list @code{plot_options} 
(after modifying one of its elements).

See also @code{plot_options}, @code{plot2d}, and @code{plot3d}.

Examples:

Modify the @code{grid} and @code{x} values.
When a @code{plot_options} keyword has an assigned value,
quote it to prevent evaluation.

@c ===beg===
@c set_plot_option ([grid, 30, 40]);
@c x: 42;
@c set_plot_option (['x, -100, 100]);
@c ===end===

@example
(%i1) set_plot_option ([grid, 30, 40]);
(%o1) [[x, - 1.755559702014E+305, 1.755559702014E+305], 
[y, - 1.755559702014E+305, 1.755559702014E+305], [t, - 3, 3], 
[grid, 30, 40], [transform_xy, false], [run_viewer, true], 
[plot_format, gnuplot], [gnuplot_term, default], 
[gnuplot_out_file, false], [nticks, 10], [adapt_depth, 10], 
[gnuplot_pm3d, false], [gnuplot_preamble, ], 
[gnuplot_curve_titles, [default]], 
[gnuplot_curve_styles, [with lines 3, with lines 1, 
with lines 2, with lines 5, with lines 4, with lines 6, 
with lines 7]], [gnuplot_default_term_command, ], 
[gnuplot_dumb_term_command, set term dumb 79 22], 
[gnuplot_ps_term_command, set size 1.5, 1.5;set term postscript #
eps enhanced color solid 24]]
(%i2) x: 42;
(%o2)                          42
(%i3) set_plot_option (['x, -100, 100]);
(%o3) [[x, - 100.0, 100.0], [y, - 1.755559702014E+305, 
1.755559702014E+305], [t, - 3, 3], [grid, 30, 40], 
[transform_xy, false], [run_viewer, true], 
[plot_format, gnuplot], [gnuplot_term, default], 
[gnuplot_out_file, false], [nticks, 10], [adapt_depth, 10], 
[gnuplot_pm3d, false], [gnuplot_preamble, ], 
[gnuplot_curve_titles, [default]], 
[gnuplot_curve_styles, [with lines 3, with lines 1, 
with lines 2, with lines 5, with lines 4, with lines 6, 
with lines 7]], [gnuplot_default_term_command, ], 
[gnuplot_dumb_term_command, set term dumb 79 22], 
[gnuplot_ps_term_command, set size 1.5, 1.5;set term postscript #
eps enhanced color solid 24]]
@end example

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

@subsection Functions for working with the gnuplot_pipes format

@deffn {Function} gnuplot_start ()

Opens the pipe to gnuplot used for plotting with the
@code{gnuplot_pipes} format. Is not necessary to manually open the
pipe before plotting.

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

@deffn {Function} gnuplot_close ()

Closes the pipe to gnuplot which is used with the @code{gnuplot_pipes}
format.

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

@deffn {Function} gnuplot_restart ()

Closes the pipe to gnuplot which is used with the @code{gnuplot_pipes}
format and opens a new pipe.

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

@deffn {Function} gnuplot_replot ()
@deffnx {Function} gnuplot_replot (@var{s})

Updates the gnuplot window. If @code{gnuplot_replot} is called with a
gnuplot command in a string @var{s}, then @code{s} is sent to gnuplot
before reploting the window.

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

@deffn {Function} gnuplot_reset ()

Resets the state of gnuplot used with the @code{gnuplot_pipes}
format. To update the gnuplot window call @code{gnuplot_replot} after
@code{gnuplot_reset}.

@opencatbox
@category{Plotting}
@closecatbox

@end deffn

